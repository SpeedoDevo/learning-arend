\import Basics
\import Proofs
\import Equality

\module CommutativityOf+ \where {
  \open Nat (+)
  \open SymmetryTransitivityLeibniz (pmap, inv)

  \func transport {A : \Type} (B : A -> \Type) {a a' : A} (p : a = a') (b : B a) =>
    coe (\lam i => B (p @ i)) b right

  \func \infixr 5 *> {A : \Type} {a a' a'' : A} (p : a = a') (q : a' = a'') : a = a'' =>
    transport (\lam x => a = x) q p

  \func +-comm (n m : Nat) : n + m = m + n
    | 0, 0 => idp
    | suc n, 0 => pmap suc (+-comm n 0)
    | 0, suc m => pmap suc (+-comm 0 m)
    | suc n, suc m =>
      \let
        | p => +-comm (suc n) m
        | q => pmap suc (inv (+-comm n m))
        | r => +-comm n (suc m)
        | s => p *> q *> r
      \in pmap suc s
}

\module EquationalReasoning \where {
  \open CommutativityOf+ (*>)
  \open Nat (+)
  \open SymmetryTransitivityLeibniz (pmap, inv)

  \func \fix 2 qed {A : \Type} (a : A) : a = a => idp

  \func \infixr 1 >== {A : \Type} {a a' a'' : A} (p : a = a') (q : a' = a'') => p *> q

  \func \infix 2 ==< {A : \Type} (a : A) {a' : A} (p : a = a') => p

  \func +-comm' (n m : Nat) : n + m = m + n
    | 0, 0 => idp
    | suc n, 0 => pmap suc (+-comm' n 0)
    | 0, suc m => pmap suc (+-comm' 0 m)
    | suc n, suc m => pmap suc (
      suc n + m   ==< +-comm' (suc n) m >==
      suc (m + n) ==< pmap suc (inv (+-comm' n m)) >==
      suc (n + m) ==< +-comm' n (suc m) >==
      suc m + n   `qed
    )
}

\module JOperator \where {
  \func transport' {A : \Type}
                   (B : A -> \Type)
                   {a a' : A} (p : a = a')
                   (b : B a) : B a' =>
    coe (\lam i => B (p @ i)) b right

  \func J {A : \Type} {a : A}
          (B : \Pi (a' : A) -> a = a' -> \Type)
          (b : B a idp)
          {a' : A} (p : a = a') : B a' p =>
    coe (\lam i => B (p @ i) (psqueeze p i)) b right
    \where
      \func psqueeze {A : \Type} {a a' : A}
                     (p : a = a')
                     (i : I) : a = p @ i =>
        path (\lam j => p @ I.squeeze i j)

  --  \func K {A : \Type} {a : A}
  --          (B : a = a -> \Type)
  --          (b : B idp)
  --          (p : a = a) : B p =>
  --    {?}

  \func transport {A : \Type} (B : A -> \Type) {a a' : A} (p : a = a') (b : B a) : B a' \elim p
    | idp => b

  \module Exercise1 \where {
    \func Jalt {A : \Type}
               (B : \Pi (a a' : A) -> a = a' -> \Type)
               (b : \Pi (a : A) -> B a a idp)
               {a a' : A} (p : a = a') : B a a' p =>
      J (\lam b' q => B a b' q) (b a) p

    \func S=S {A B : \Type} {a a' : A} {b b' : B}
              (p : a = a')
              (q : b = b') : (a, b) = (a', b') \elim p, q
      | idp, idp => idp

    \func Eq {A : \Type} (a : A) => \Sigma (x : A) (a = x)

    \func p->idp {A : \Type} {a : A} (p : Eq a) : (a, idp) = {Eq a} p =>
      Jalt (\lam a x a=x => (a, idp) = {Eq a} (x, a=x)) (\lam _ => idp) p.2

    \func J' {A : \Type} {a : A}
             (B : \Pi (a' : A) -> a = a' -> \Type)
             (b : B a idp)
             {a' : A} (p : a = a') : B a' p =>
      transport (\lam a => B a.1 a.2) (p->idp (a', p)) b
  }
}

\module AssociativityOfAppend \where {
  \open Nat (+)
  \open SymmetryTransitivityLeibniz (transport, pmap, inv)
  \open CommutativityOf+ (*>)

  \data Vec (A : \Type) (n : Nat) \elim n
    | zero => vnil
    | suc n => vcons A (Vec A n)

  \func \infixl 4 v++ {A : \Type} {n m : Nat} (xs : Vec A n) (ys : Vec A m) : Vec A (m + n) \elim n, xs
    | 0, vnil => ys
    | suc n, vcons x xs => vcons x (xs v++ ys)

  \func +-assoc (x y z : Nat) : (x + y) + z = x + (y + z) \elim z
    | 0 => idp
    | suc z => pmap suc (+-assoc x y z)

  \func v++-assoc {A : \Type} {n m k : Nat} (xs : Vec A n) (ys : Vec A m) (zs : Vec A k)
    : (xs v++ ys) v++ zs = transport (Vec A) (+-assoc k m n) (xs v++ (ys v++ zs)) \elim n, xs
    | 0, vnil => idp
    | suc n, vcons x xs =>
      pmap (vcons x) (v++-assoc xs ys zs) *>
      inv (transport-vcons-comm (+-assoc k m n) x (xs v++ (ys v++ zs)))

  \func transport-vcons-comm {A : \Type} {n m : Nat} (p : n = m) (x : A) (xs : Vec A n)
    : transport (Vec A) (pmap suc p) (vcons x xs) = vcons x (transport (Vec A) p xs)
    | idp, _, _ => idp

  \module Exercise2 \where {
    \func z+-is-id {n : Nat} : 0 + n = n
      | {0} => idp
      | {suc n} => pmap suc z+-is-id

    \func v++-vnil-is-id {A : \Type} {n : Nat} (v : Vec A n)
      : v = transport (Vec A) z+-is-id (v v++ vnil) \elim n, v
      | 0, vnil => idp
      | suc n, vcons a v =>
        \let
          | rec => v++-vnil-is-id v
          | mapped => pmap (vcons a) rec
          | transported => transport-vcons-comm z+-is-id a (v v++ vnil)
        \in mapped *> (inv transported)
  }
}

\module Predicates \where {
  \open Data (Unit, unit, Empty)
  \open Nat (+)
  \open SymmetryTransitivityLeibniz (pmap)

  -- Definition of <= via equality.

  \func LessOrEq''' (n m : Nat) => \Sigma (k : Nat) (k + n = m)

  -- Recursive definition of <=.

  \func lessOrEq (n m : Nat) : \Type
    | 0, _ => Unit
    | suc _, 0 => Empty
    | suc n, suc m => lessOrEq n m

  -- First inductive definition of <=.

  \data LessOrEq (n m : Nat) \with
    | 0, m => z<=n
    | suc n, suc m => s<=s (LessOrEq n m)

  \func test11 : LessOrEq 0 100 => z<=n

  \func test12 : LessOrEq 3 67 => s<=s (s<=s (s<=s z<=n))

  -- Of course, there is no proof of 1 <= 0.
  -- \func test10 : LessOrEq 1 0 => ...

  -- Second inductive definition of <=.
  -- This is a modification of the first inductive definition,
  -- where we avoid constructor patterns.

  \data LessOrEq' (n m : Nat)
    | z<=n' (n = 0)
    | s<=s' {n' m' : Nat} (n = suc n') (m = suc m') (LessOrEq' n' m')

  -- Third inductive definition of <=.

  \data LessOrEq'' (n m : Nat) \elim m
    | suc m => <=-step (LessOrEq'' n m)
    | m => <=-refl (n = m)

  \module Exercise3 \where {
    \open CurryHoward (absurd)
    \open SymmetryTransitivityLeibniz (transport, inv)

    \func \infix 1 <-> (A B : \Type) => \Sigma (A -> B) (B -> A)

    \func proof1 {n m : Nat} : LessOrEq n m <-> lessOrEq n m => (left, right)
      \where {
        \func left {n m : Nat} (p : LessOrEq n m) : lessOrEq n m \elim n, m, p
          | 0, 0, z<=n => unit
          | 0, suc m, z<=n => unit
          | suc n, suc m, s<=s p => left p

        \func right {n m : Nat} (p : lessOrEq n m) : LessOrEq n m \elim n, m, p
          | 0, 0, unit => z<=n
          | 0, suc m, unit => z<=n
          | suc n, suc m, p => s<=s (right p)
      }

    \func proof2 {n m : Nat} : LessOrEq n m <-> LessOrEq' n m => (left, right)
      \where {
        \func left {n m : Nat} (p : LessOrEq n m) : LessOrEq' n m \elim n, m, p
          | 0, 0, z<=n => z<=n' idp
          | 0, suc m, z<=n => z<=n' idp
          | suc n, suc m, s<=s p => s<=s' idp idp (left p)

        \func right {n m : Nat} (p : LessOrEq' n m) : LessOrEq n m \elim n, p
          | 0, z<=n' p => z<=n
          | 0, s<=s' p p1 p2 => z<=n
          | suc n, z<=n' p => suc-n/=0 p
          | suc n, s<=s' p idp p2 => transport (\lam n' => LessOrEq n' m) (inv p) (s<=s (right p2))
      }

    \func proof3 {n m : Nat} : LessOrEq n m <-> LessOrEq'' n m => (left, right)
      \where {
        \func left {n m : Nat} (p : LessOrEq n m) : LessOrEq'' n m \elim n, m, p
          | 0, 0, z<=n => <=-refl idp
          | 0, suc m, z<=n => <=-step (left z<=n)
          | suc n, suc m, s<=s p => map-suc (left p)
          \where
            \func map-suc {n m : Nat} (p : LessOrEq'' n m) : LessOrEq'' (suc n) (suc m) \elim m, p
              | 0, <=-refl p => <=-refl (pmap suc p)
              | suc m, <=-step p => <=-step (map-suc p)
              | suc m, <=-refl p => <=-refl (pmap suc p)

        \func right {n m : Nat} (p : LessOrEq'' n m) : LessOrEq n m \elim n, m, p
          | 0, _, _ => z<=n
          | suc n, 0, <=-refl p => suc-n/=0 p
          | suc n, suc m, <=-step p => step-LessOrEq (right p)
          | suc n, suc m, <=-refl p => refl-LessOrEq p
          \where {
            \func step-LessOrEq {n m : Nat} (p : LessOrEq n m) : LessOrEq n (suc m) \elim n, m, p
              | 0, _, _ => z<=n
              | suc n, suc m, s<=s p => s<=s (step-LessOrEq p)

            \func refl-LessOrEq {n m : Nat} (p : n = m) : LessOrEq n m \elim n, m
              | 0, _ => z<=n
              | suc n, 0 => transport (\lam n' => LessOrEq n' 0) (inv p) z<=n
              | suc n, suc m => s<=s (refl-LessOrEq (pmap cus p))
          }
      }

    \func proof4 {n m : Nat} : LessOrEq n m <-> LessOrEq''' n m => (left, right)
      \where {
        \func left {n m : Nat} (p : LessOrEq n m) : LessOrEq''' n m \elim n, m, p
          | 0, 0, z<=n => (0, idp)
          | 0, suc m, z<=n => (suc m, idp)
          | suc n, suc m, s<=s p => \let (k, q) => left p \in (k, pmap suc q)

        \func right {n m : Nat} (p : LessOrEq''' n m) : LessOrEq n m \elim n, m, p
          | 0, _, _ => z<=n
          | suc n, 0, p => suc-n/=0 p.2
          | suc n, suc m, p => s<=s (right (p.1, pmap cus p.2))
      }

    \func cus (n : Nat) : Nat
      | 0 => 0
      | suc n => n

    \func suc-n/=0 {n : Nat} (p : suc n = 0) {A : \Type} : A =>
      absurd (NonEqualities.Exercise13.proof (inv p))
  }

  \module Exercise4 \where {
    \open Lists (List, cons, nil, /)

    \data In {A : \Type} (x : A) (xs : List A) \elim xs
      | cons x' xs => here (x = x')
      | cons x' xs => there (In x xs)

    \func test-In => ()
      \where {
        \func list => 1 / 2 / 3 / nil

        \func In1 : In 1 list => here idp

        \func In2 : In 2 list => there (here idp)

        \func In3 : In 3 list => there (there (here idp))

        \func In-nil {A : \Type} {x : A} (p : In x nil) {B : \Type} : B
      }
  }

  \module Exercise5 \where {
    \data ReflTransClosure {A : \Type} (R : A -> A -> \Type) (x y : A)
      | refl (x = y)
      | trans (z : A) (R x z) (ReflTransClosure R z y)
  }

  \module Exercise6 \where {
    \open SymmetryTransitivityLeibniz (transport)
    \open Exercise5 (ReflTransClosure, refl, trans)

    \func \infix 4 <-> {A : \Type} (P Q : A -> A -> \Type) =>
      \Pi (x y : A) -> \Sigma (P x y -> Q x y) (Q x y -> P x y)

    \func ReflTransClosure-lem {A : \Type} (R : A -> A -> \Type)
                               (r : \Pi (x : A) -> R x x)
                               (t : \Pi (x y z : A) -> R x y -> R y z -> R x z)
      : R <-> ReflTransClosure R =>
      \lam x y => (left r t, right r t)
      \where {
        \func left {A : \Type} {R : A -> A -> \Type}
                   (r : \Pi (x : A) -> R x x)
                   (t : \Pi (x y z : A) -> R x y -> R y z -> R x z)
                   {x y : A}
                   (p : R x y)
          : ReflTransClosure R x y =>
          trans y p (refl idp)

        \func right {A : \Type} {R : A -> A -> \Type}
                    (r : \Pi (x : A) -> R x x)
                    (t : \Pi (x y z : A) -> R x y -> R y z -> R x z)
                    {x y : A}
                    (p : ReflTransClosure R x y)
          : R x y \elim p
          | refl p => transport (R x) p (r x)
          | trans z q p => t x z y q (right r t p)
      }
  }

  \module Exercise7 \where {
    \open Lists (List, cons, nil, /)

    \data \infix 4 <= {A : \Type} (xs : List A) (ys : List A) \elim xs, ys
      | nil, _ => nil<=_
      | cons x xs, cons y ys => here (x = y) (xs <= ys)
      | xs, cons y ys => there (xs <= ys)

    \func test-<= => ()
      \where {
        \func A => 1 / 2 / 3 / nil

        \func B => 2 / 3 / nil

        \func B<=A : B <= A => there (here idp (here idp nil<=_))
      }
  }

  \module Exercise8 \where {
    \open Data (Bool, true, false)
    \open Lists (List, cons, nil, /)
    \open Exercise7

    \func filter {A : \Type} (fn : A -> Bool) (xs : List A) : List A \elim xs
      | nil => nil
      | cons a xs => filter-cons (fn a) a (filter fn xs)
      \where {
        \func filter-cons {A : \Type} (b : Bool) (x : A) (ys : List A) : List A \elim b
          | true => x / ys
          | false => ys
      }

    \func proof {A : \Type} (fn : A -> Bool) (xs : List A) : filter fn xs <= xs \elim xs
      | nil => nil<=_
      | cons a xs => proof-cons (fn a) a (filter fn xs) xs (proof fn xs)
      \where {
        \func proof-cons {A : \Type} (b : Bool) (a : A) (xs ys : List A) (p : xs <= ys)
          : filter.filter-cons b a xs <= cons a ys \elim b
          | true => here idp p
          | false => there p
      }
  }
}
