\import Case
\import Equality
\import Proofs
\import ProofsOfEquality
\import Basics

\module Hierarchies \where {
  \func tt : \Type2 => \Type0 -> \Type1

  \func id (A : \Type) (a : A) => a

  \func id' (A : \Type \lp) (a : A) => a

  \func type : \Type => \Type

  \func type' : \Type (\suc \lp) => \Type \lp

  \func test0 : \Type (\max (\suc (\suc \lp)) 4) => \Type (\max \lp 3) -> \Type (\suc \lp)

  \func test1 => id Nat 0

  \func test2 => id \Type0 Nat

  \func test3 => id (\Type0 -> \Type1) (\lam X => X)

  \func test4 => id _ id

  \func test4' => id (\Pi (A : \Type) -> A -> A) id

  \func test5 => id (\suc \lp) (\Type \lp) Nat

  \func test5' => id (\levels (\suc \lp) _) (\Type \lp) Nat

  \func test6 => id (\levels 2 _) \Type1 \Type0

  \data Magma (A : \Type)
    | con (A -> A -> A)

  \data MagmaEx (A : \Type) (B : \Type5)
    | con' (A -> A -> A)

  \func test7 : \Type \lp => MagmaEx \lp Nat \Type4

  \class Magma' (A : \Type)
    | \infixl 6 ** : A -> A -> A

  \func test8 : \Type (\suc \lp) => Magma' \lp

  \func test9 : \Type \lp => Magma \lp Nat

  \class Functor (F : \Type -> \Type)
    | fmap {A B : \Type} : (A -> B) -> F A -> F B

  \data Maybe (A : \Type) | nothing | just A

  \func test10 : \Type (\suc \lp) => Functor \lp Maybe

  \module Exercise1 \where {
    \func id'' {A : \Type} (a : A) => a

    \func idTest1 => id'' (\suc \lp) (id'' (\suc \lp) id)

    \func idTest2 => id'' (\suc \lp) Maybe

    \func idTest3 => id'' (\levels 2 _) Functor

    \func idTest4 => id'' (\levels 2 _) (Functor Maybe)

    \func idTest5 : \Pi (f : \Pi {A B : \Type} -> (A -> B) -> Maybe A -> Maybe B) -> \Prop =>
      \lam f => id'' (\suc \lp) (Functor Maybe f)
  }
}

\module Induction \where {
  \open Views.Exercise4 (<, z<s, s<s, compare, lt, eq, gt, <absurd)
  \open SymmetryTransitivityLeibniz (transport, inv)

  \func <0 {n : Nat} (p : n < 0) {A : \Type} : A

  \func n<suc_n {n : Nat} : n < suc n \elim n
    | 0 => z<s
    | suc n => s<s n<suc_n

  \func m<k<n {m k n : Nat} (p : m < suc k) (q : k < n) : m < n \elim m, k, n, p, q
    | 0, 0, 0, z<s, ()
    | 0, 0, suc n, z<s, z<s => z<s
    | 0, suc k, 0, z<s, ()
    | 0, suc k, suc n, z<s, s<s q => z<s
    | suc m, 0, 0, s<s p, ()
    | suc m, 0, suc n, s<s (), _
    | suc m, suc k, 0, s<s p, ()
    | suc m, suc k, suc n, s<s p, s<s q => s<s (m<k<n p q)

  \func Nat-ind (E : Nat -> \Type)
                (r : \Pi (n : Nat) -> (\Pi (k : Nat) -> k < n -> E k) -> E n)
                (n : Nat) : E n => Ek E r (suc n) n n<suc_n
    \where {
      \func Ek (E : Nat -> \Type)
               (r : \Pi (n : Nat) -> (\Pi (k : Nat) -> k < n -> E k) -> E n)
               (n k : Nat)
               (l : k < n) : E k \elim n, k, l
        | 0, 0, ()
        | suc n, 0, z<s => r 0 (\lam k lt => <0 lt)
        | suc n, suc k, s<s l => r (suc k) (\lam m p => Ek E r n m (m<k<n p l))
    }

  \module Exercise2 \where {
    \func div (m n : Nat) (p : 0 < n) : Nat =>
      Nat-ind (\lam _ => Nat) (
        \lam m r =>
            \case compare m n \with {
              | lt l => 0
              | eq e => 1
              | gt g => suc (r (sub m n g) (sub_n<n m n g p))
            }
      ) m
      \where {
        \func sub (m n : Nat) (p : n < m) : Nat
          | 0, 0, ()
          | suc m, 0, z<s => suc m
          | suc m, suc n, s<s p => sub m n p

        \func test-sub : sub 5 3 (s<s (s<s (s<s z<s))) = 2 => idp

        \func test-sub' : sub 8 3 (s<s (s<s (s<s z<s))) = 5 => idp

        \func sub_n<n (m n : Nat) (p : n < m) (nz : 0 < n) : sub m n p < m
          | 0, 0, nz, ()
          | suc m, 0, z<s, ()
          | suc m, suc n, s<s p, z<s =>
            \case compare 0 n \with {
              | lt l => m<n->m<suc_n (sub_n<n m n p l)
              | eq e => transport (\lam k => k < suc m) (m=sub_m_0 e p) n<suc_n
              | gt ()
            }

        \func m=sub_m_0 {m n : Nat} (z : 0 = n) (n<m : n < m) : m = sub m n n<m \elim m, n, z, n<m
          | 0, 0, z, ()
          | 0, suc n, z, ()
          | suc m, 0, z, z<s => idp
          | suc m, suc n, z, s<s n<m => suc-n/=0 (inv z)
          \where \open Predicates.Exercise3 (suc-n/=0)

        \func m<n->m<suc_n {m n : Nat} (p : m < n) : m < suc n \elim m, n, p
          | 0, suc n, z<s => z<s
          | suc m, suc n, s<s p => s<s (m<n->m<suc_n p)
      }

    \func test-div : div 9 3 z<s = 3 => idp

    \func test-div' : div 3 9 z<s = 0 => idp

    \func test-div'' : div 144 12 z<s = 12 => idp
  }

  \module Exercise3 \where {
    \open Lists (List, cons, nil, /)
    \open FilterProof (length)

    \func List-ind {A : \Type} (E : List A -> \Type)
                   (r : \Pi (xs : List A) -> (\Pi (ys : List A) -> length ys < length xs -> E ys) -> E xs)
                   (xs : List A) : E xs \elim xs
      | nil => r nil (\lam zs p => <0 p)
      | cons x xs => Eys E r (x / x / xs) (x / xs) (n<suc_n)
      \where {
        \func Eys {A : \Type} (E : List A -> \Type)
                  (r : \Pi (xs : List A) -> (\Pi (ys : List A) -> length ys < length xs -> E ys) -> E xs)
                  (xs ys : List A)
                  (l : length ys < length xs) : E ys \elim xs, ys, l
          | nil, nil, ()
          | cons x xs, nil, z<s => r nil (\lam zs p => <0 p)
          | cons x xs, cons y ys, s<s l => r (cons y ys) (\lam zs p => Eys E r xs zs (m<k<n p l))
      }
  }
}

\module InductionRecursion \where {
  \open Data (Bool, true, false)
  \open Lists (List)

  \func isEven (n : Nat) : Bool
    | 0 => true
    | suc n => isOdd n

  \func isOdd (n : Nat) : Bool
    | 0 => false
    | suc n => isEven n

  \data IsEven (n : Nat) : \Type \with
    | 0 => zero-isEven
    | suc n => suc-isEven (IsOdd n)

  \data IsOdd (n : Nat) : \Type \with
    | suc n => suc-isOdd (IsEven n)

  \data Type
    | nat
    | list Type
    | arr Type Type

  --  \func El (t : Type) : \Type0 \elim t
  --    | nat => Nat
  --    | list t => List (El t)
  --    | arr t1 t2 => El t1 -> El t2

  --  \func idc (t : Type) (x : El t) : El t => x

  --  \data Type' : \Set0
  --    | nat'
  --    | list' Type'
  --    | pi' (a : Type') (El' a -> Type')

  --  \func El' (t : Type') : \Type0 \elim t
  --    | nat' => Nat
  --    | list' t => List (El' t)
  --    | pi' t1 t2 => \Pi (a : El' t1) -> El' (t2 a)
}

\module Completeness \where {
  \open Data (Empty)
  \open Nat (*)
  \open Lists (List)

  -- P1 is correct specification for 'fac', but incomplete.

  \func P1 (f : Nat -> Nat) => f 3 = 6

  -- P2 is complete, but not correct.

  \func P2 (f : Nat -> Nat) => Empty

  -- P3 -- correct and complete specification for 'fac'.

  \func P3 (f : Nat -> Nat) => \Sigma (f 0 = 1) (\Pi (n : Nat) -> f (suc n) = suc n * f n)

  \func isSorted {A : \Type} (x : List A) : \Type => Empty

  \func isPerm {A : \Type} (x y : List A) : \Type => Empty

  \func P {A : \Type} (f : List A -> List A) => \Pi (xs : List A) -> \Sigma (isSorted (f xs)) (isPerm (f xs) xs)

  -- where 'isSorted xs' is true iff  'xs' is sorted and
  -- 'isPerm xs ys' is true iff 'xs' is a permutation of 'ys'.

  -- One useful necessary and sufficient condition of completeness for correct specifications can be formulated as follows:
  -- \Pi (x y : A) -> P x -> P y -> x = y

  \module Exercise4 \where {
    \open Lists (List, cons, nil, /)
    \open Data (Bool, true, false, unit)
    \open Propositions (T)
    \open SymmetryTransitivityLeibniz (transport, inv)
    \open Predicates.Exercise4 (In)
    \open Predicates.Exercise7 (<=, nil<=_, here, there)
    \open Predicates.Exercise8 \using (filter, proof \as proof-a)

    \func proof-b {A : \Type} (fn : A -> Bool) (xs : List A) (x : A) (in : x `In` filter fn xs) : T (fn x) \elim xs, in
      | cons x' xs, in =>
        \case fn x' \as b, idp : b = fn x', in : x `In` filter-cons b x' (filter fn xs)
        \with {
          | true, e, In.here p =>
            \let t => transport (\lam t => T t) e unit
            \in transport (\lam b => T (fn b)) (inv p) t
          | true, e, In.there i => proof-b fn xs x i
          | false, e, i => proof-b fn xs x i
        }
      \where \open filter

    \func proof-c {A : \Type} (fn : A -> Bool) (xs ys : List A) (p : ys <= xs)
                  (q : \Pi (y : A) (y `In` ys) -> T (fn y)) : ys <= filter fn xs \elim xs, ys, p
      | _, nil, _ => nil<=_
      | cons x xs, cons y ys, here y=x ys<=xs =>
        \let t => transport (\lam a => T (fn a)) y=x (q y (In.here idp))
        \in \case fn x \as b, t : T b
        \return cons y ys <= filter-cons b x (filter fn xs)
        \with {
          | true, unit => here y=x (proof-c fn xs ys ys<=xs (\lam y' in => q y' (In.there in)))
        }
      | cons x xs, cons y ys, there p =>
        \case fn x \as b
        \return cons y ys <= filter-cons b x (filter fn xs)
        \with {
          | true => there (proof-c fn xs (y / ys) p q)
          | false => proof-c fn xs (y / ys) p q
        }
      \where \open filter

    \func proof {A : \Type} (fn : A -> Bool) (xs : List A) : \Sigma
      (filter fn xs <= xs)
      (\Pi (x : A) (in : x `In` filter fn xs) -> T (fn x))
      (\Pi (ys : List A) (p : ys <= xs) (q : \Pi (y : A) (y `In` ys) -> T (fn y)) -> ys <= filter fn xs) =>
      ( proof-a fn xs
      , proof-b fn xs
      , proof-c fn xs
      )
  }
}

\module CorrectnessOfInsertionSort \where {
  \open Lists (List, cons, nil, /)
  \open CurryHoward (||, inl, inr)

  \class Preorder (E : \Type)
    | \infix 4 <= : E -> E -> \Type
    | <=-refl {x : E} : x <= x
    | <=-trans {x y z : E} : x <= y -> y <= z -> x <= z

  \class TotalPreorder \extends Preorder
    | totality (x y : E) : (x <= y) || (y <= x)

  \func sort {A : TotalPreorder} (xs : List A) : List A
    | nil => nil
    | cons a xs => insert a (sort xs)
    \where
      \func insert {A : TotalPreorder} (a : A) (xs : List A) : List A \elim xs
        | nil => a / nil
        | cons x xs => \case totality x a \with {
          | inl _ => x / insert a xs
          | inr _ => a / x / xs
        }

  \data Perm {A : \Type} (xs ys : List A) \elim xs, ys
    | nil, nil => perm-nil
    | cons x xs, cons y ys => perm-cons (x = y) (Perm xs ys)
    | xs, ys => perm-trans {zs : List A} (Perm xs zs) (Perm zs ys)
    | cons x (cons x' xs), cons y (cons y' ys) => perm-perm (x = y') (x' = y) (xs = ys)
    \where {
      \func perm-refl {A : \Type} {xs : List A} : Perm xs xs \elim xs
        | nil => perm-nil
        | cons x xs => perm-cons idp perm-refl

      \func perm-swap {A : \Type} {x x' : A} {xs ys : List A} (p : Perm (x / x' / xs) ys) : Perm (x' / x / xs) ys =>
        perm-trans (perm-perm idp idp idp) p
    }

  \func sort-perm {A : TotalPreorder} (xs : List A) : Perm xs (sort xs) \elim xs
    | nil => perm-nil
    | cons x xs => perm-trans (perm-cons idp (sort-perm xs)) (insert-perm x (sort xs))
    \where {
      \open sort
      \open Perm

      \func insert-perm {A : TotalPreorder} (a : A) (xs : List A) : Perm (a / xs) (insert a xs) \elim xs
        | nil => perm-cons idp perm-nil
        | cons x xs =>
          \case totality x a \as txa
          \return Perm (a / cons x xs) (\case txa \with {
            | inl _ => x / insert a xs
            | inr _ => a / x / xs
          })
          \with {
            | inl _ => perm-trans (perm-perm idp idp idp) (perm-cons idp (insert-perm a xs))
            | inr _ => perm-refl
          }
    }

  --  \data IsSorted {A : Preorder} (xs : List A) \with
  --    | nil => nil-sorted
  --    | cons _ nil => single-sorted
  --    | cons x (cons y _ \as xs) => cons-sorted (x <= y) (IsSorted xs)

  \func head {A : \Type} (def : A) (xs : List A) : A \elim xs
    | nil => def
    | cons a _ => a

  \data IsSorted {A : Preorder} (xs : List A) \elim xs
    | nil => nil-sorted
    | cons x xs => cons-sorted (x <= head x xs) (IsSorted xs)

  \func sort-sorted {A : TotalPreorder} (xs : List A) : IsSorted (sort xs) \elim xs
    | nil => nil-sorted
    | cons a l => insert-sorted a (sort-sorted l)
    \where {
      \open sort

      \func insert-sorted {A : TotalPreorder} (x : A)
                          {xs : List A} (xs-sorted : IsSorted xs) : IsSorted (insert x xs) \elim xs
        | nil => cons-sorted <=-refl nil-sorted
        | cons a l =>
          \case totality a x \as r
          \return
          IsSorted (\case r \with {
            | inl _ => a / insert x l
            | inr _ => x / a / l
          })
          \with {
            | inl a<=x =>
              \case xs-sorted
              \with {
                | cons-sorted a<=l l-sorted => cons-sorted (insert-lem a x l a<=x a<=l) (insert-sorted x l-sorted)
              }
            | inr x<=a => cons-sorted x<=a xs-sorted
          }

      \func insert-lem {A : TotalPreorder} (a x : A) (l : List A) (a<=x : a <= x) (a<=l : a <= head a l)
        : a <= head a (insert x l) \elim l
        | nil => a<=x
        | cons b l =>
          \case totality b x \as r
          \return
          a <= head a (
            \case r
            \with {
              | inl _ => b / insert x l
              | inr _ => x / b / l
            }
          )
          \with {
            | inl _ => a<=l
            | inr _ => a<=x
          }
    }

  \module Exercise5 \where {
    \instance TotalPreorderNat : TotalPreorder Nat
      | <= => <=
      | <=-refl => refl
      | <=-trans => trans
      | totality => totality
      \where {
        \open Nat

        \func refl {n : Nat} : n <= n \elim n
          | 0 => zero<=_
          | suc n => suc<=suc refl

        \func trans {x y z : Nat} (p : x <= y) (q : y <= z) : x <= z \elim x, y, z
          | 0, _, _ => zero<=_
          | suc x, 0, 0 => p
          | suc x, 0, suc z =>
            \case \elim p
            \with {
            }
          | suc x, suc y, 0 =>
            \case \elim q
            \with {
            }
          | suc x, suc y, suc z => \case \elim p, \elim q \with {
            | suc<=suc p, suc<=suc q => suc<=suc (trans p q)
          }

        \func totality (x y : Nat) : x <= y || y <= x
          | 0, 0 => inl zero<=_
          | 0, suc y => inl zero<=_
          | suc x, 0 => inr zero<=_
          | suc x, suc y => \case totality x y \with {
            | inl p => inl (suc<=suc p)
            | inr q => inr (suc<=suc q)
          }
      }

    \func msort {A : TotalPreorder} (xs : List A) : List A => merge-all (split xs)
      \where {
        \func split {A : \Type} (xs : List A) : List (List A) \elim xs
          | nil => nil
          | cons x xs => (x / nil) / split xs

        \func test-split : split (1 / 2 / 3 / nil) = (1 / nil) / (2 / nil) / (3 / nil) / nil => idp

        \func merge {A : TotalPreorder} (xs ys : List A) : List A \elim xs, ys
          | nil, nil => nil
          | nil, cons y ys => y / ys
          | cons x xs, nil => x / xs
          | cons x xs, cons y ys => \case totality x y \with {
            | inl p => x / merge xs (y / ys)
            | inr q => y / x / merge xs ys
          }

        \func test-merge : merge (1 / 3 / nil) (2 / 4 / nil) = 1 / 2 / 3 / 4 / nil => idp

        \func test-merge' : merge (1 / 2 / nil) (3 / 4 / nil) = 1 / 2 / 3 / 4 / nil => idp

        \func merge-all {A : TotalPreorder} (xss : List (List A)) : List A \elim xss
          | nil => nil
          | cons xs xss => merge (merge-all xss) xs

        \func test-merge-all : merge-all ((1 / nil) / (2 / nil) / (3 / nil) / nil) = 1 / 2 / 3 / nil => idp

        \func test-msort : msort (4 / 3 / 2 / 1 / nil) = 1 / 2 / 3 / 4 / nil => idp

        \func test-msort' :
          msort (91 / 47 / 51 / 18 / 38 / 5 / 100 / nil)
            =
          (5 / 18 / 38 / 47 / 51 / 91 / 100 / nil) => idp
      }

    \func msort-perm {A : TotalPreorder} (xs : List A) : Perm xs (msort xs) => merge-all-split-perm xs
      \where {
        \open msort
        \open Perm

        \func merge-nil-perm {A : TotalPreorder} (xs : List A) : Perm xs (merge xs nil) \elim xs
          | nil => perm-nil
          | cons x xs => perm-refl

        \func merge-single-perm {A : TotalPreorder} (x : A) (xs : List A) : Perm (x / xs) (merge xs (x / nil)) \elim xs
          | nil => perm-refl
          | cons x' xs =>
            \case totality x' x \as t
            \return Perm (x / cons x' xs) (
              \case t
              \with {
                | inl _ => x' / merge xs (x / nil)
                | inr _ => x / x' / merge xs nil
              }
            )
            \with {
              | inl _ => perm-swap (perm-cons idp (merge-single-perm x xs))
              | inr _ => perm-cons idp (perm-cons idp (merge-nil-perm xs))
            }

        \func merge-all-split-perm {A : TotalPreorder} (xs : List A) : Perm xs (merge-all (split xs)) \elim xs
          | nil => perm-nil
          | cons x xs =>
            perm-trans (perm-cons idp (merge-all-split-perm xs)) (merge-single-perm x (merge-all (split xs)))
      }

    \func msort-sorted {A : TotalPreorder} (xs : List A) : IsSorted (msort xs) => merge-all-split-sorted xs
      \where {
        \open msort
        \open SymmetryTransitivityLeibniz (transport)

        \func merge-nil-refl {A : TotalPreorder} (xs : List A) : xs = merge xs nil \elim xs
          | nil => idp
          | cons x xs => idp

        \func merge-ord {A : TotalPreorder} {x x' : A} {xs : List A}
                        (o : x' <= head x' xs) (p : x' <= x)
          : x' <= head x' (merge xs (x / nil)) \elim xs
          | nil => p
          | cons a xs =>
            \case totality a x \as t
            \return x' <= head x' (
              \case t
              \with {
                | inl _ => a / merge xs (x / nil)
                | inr _ => x / a / merge xs nil
              }
            )
            \with {
              | inl l => o
              | inr r => p
            }

        \func merge-single-sorted {A : TotalPreorder} (x : A) (xs : List A) (s : IsSorted xs)
          : IsSorted (merge xs (x / nil)) \elim xs
          | nil => cons-sorted <=-refl nil-sorted
          | cons x' xs =>
            \case totality x' x \as t
            \return IsSorted (
              \case t
              \with {
                | inl _ => x' / merge xs (x / nil)
                | inr _ => x / x' / merge xs nil
              }
            )
            \with {
              | inl p =>
                \case \elim s \with {
                  | cons-sorted o s =>
                    cons-sorted (merge-ord o p) (merge-single-sorted x xs s)
                }
              | inr q => transport (\lam k => IsSorted (x / x' / k)) (merge-nil-refl xs) (cons-sorted q s)
            }

        \func merge-all-split-sorted {A : TotalPreorder} (xs : List A) : IsSorted (merge-all (split xs)) \elim xs
          | nil => nil-sorted
          | cons x xs => merge-single-sorted x (merge-all (split xs)) (merge-all-split-sorted xs)
      }
  }
}
